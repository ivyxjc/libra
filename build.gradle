plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.70' apply false
    id "org.springframework.boot" version '2.1.6.RELEASE' apply false
    id "me.champeau.gradle.jmh" version "0.4.8" apply false
    id "io.freefair.aspectj" version "5.0.0-rc6" apply false
    id "io.morethan.jmhreport" version "0.9.0" apply false
}

ext {
    base_version = '0.0.1-SNAPSHOT'

    kotlin_version = '1.3.70'
    kotlin_coroutine_version = '1.3.5'

    activemq_version = '5.15.10'
    artemis_version = '2.10.0'
    aspectj_version = '1.9.5'
    commons_lang3_version = '3.7'
    druid_version = '1.1.20'
    druid_starter_version = '1.1.20'
    gson_version = '2.8.5'
    guava_version = '28.2-jre'
    h2_version = '1.4.200'
    hibernate_version = '5.4.0.Final'
    hikaricp_version = '3.4.2'
    jaxb_api_version = '2.3.1'
    jaxb_glassfish_version = '2.3.1'
    jetbrains_annotations_version = '19.0.0'
    jmh_version = '1.23'
    jms_version = '2.0.1'
    junit_version = '4.12'
    kafka_client_version = '2.3.0'
    log4j_slf4j_version = '2.11.2'
    lombok_version = '1.18.4'
    mockito_kotlin_version = '1.6.0'
    mockito_version = '2.23.4'
    mybatis_spring_version = "2.0.2"
    mybatis_starter_version = '2.1.0'
    mybatis_version = '3.5.4'
    mysql_connector_version = '8.0.19'
    okhttp_version = '4.0.1'
    protobuf_version = '3.11.4'
    slf4j_version = '1.7.25'
    snakeyaml_version = '1.26'
    spring_version = '5.1.9.RELEASE'
    spring_boot_version = '2.1.8.RELEASE'
    spring_kafka_version = '2.2.7.RELEASE'
}


configure(allprojects) { project ->
    group = 'com.ivyxjc'
    version base_version

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    if (name != 'libra-aspect') {
        apply plugin: 'org.jetbrains.kotlin.jvm'
    }

    sourceCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/central' }
        mavenCentral()
    }

    buildDir = "${rootProject.projectDir.path}/build/${rootProject.name}/${project.name}"

    if (name == 'libra-core-mock-biz') {
        sourceSets.main.resources.srcDirs += "${rootProject.projectDir.path}/src/main/resources"
    }

    sourceSets.test.resources.srcDirs += "${rootProject.projectDir.path}/src/test/resources"

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier = 'sources'
    }

    task fatJar(type: Jar) {
        baseName = project.name
        print(configurations)
        from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
        with jar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_11// can be switched to 11 for testing
        targetCompatibility = JavaVersion.VERSION_11
        options.encoding = "UTF-8"
    }

    compileTestJava {
        sourceCompatibility = JavaVersion.VERSION_11  // can be switched to 11 for testing
        targetCompatibility = JavaVersion.VERSION_11
        options.encoding = "UTF-8"
        options.compilerArgs += "-parameters"
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-Xjsr305=strict"]
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-Xjsr305=strict"]
        }
    }

    configurations {
        all*.exclude group: 'ch.qos.logback', module: 'logback-core'
        all*.exclude group: 'ch.qos.logback', module: 'logback-classic'
        all*.exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }

    dependencies {
        testImplementation group: 'junit', name: 'junit', version: junit_version
        testImplementation(group: 'org.mockito', name: 'mockito-core', version: mockito_version) {
            exclude group: "org.hamcrest", module: "hamcrest-core"
        }
        testImplementation(group: 'com.nhaarman', name: 'mockito-kotlin', version: mockito_kotlin_version) {
            exclude module: "kotlin-stdlib"
            exclude module: "kotlin-reflect"
            exclude module: "mockito-core"
        }

        //kotlin
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
        //apache
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: commons_lang3_version
        //log
        implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
        runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j_slf4j_version

        //guava
        implementation group: 'com.google.guava', name: 'guava', version: guava_version

        compileOnly group: 'org.jetbrains', name: 'annotations', version: jetbrains_annotations_version
        testCompileOnly group: 'org.jetbrains', name: 'annotations', version: jetbrains_annotations_version


    }
}

project("libra-aspect") {
    apply plugin: 'io.freefair.aspectj'

    dependencies {
        compileOnly project(":libra-core-common")
        compileOnly group: 'org.aspectj', name: 'aspectjweaver', version: aspectj_version
    }
}

project("libra-benchmark") {

    apply plugin: "me.champeau.gradle.jmh"
    apply plugin: "io.morethan.jmhreport"

    jmhReport {
        jmhResultPath = project.file("${rootProject.projectDir.path}/build/${rootProject.name}/${project.name}/reports/jmh/results.json")
        jmhReportOutput = project.file("${rootProject.projectDir.path}/build/${rootProject.name}/${project.name}/reports/jmh/")
    }

    jmh {
        fork = 1
        threads = 1
        warmupIterations = 3
        iterations = 5
        resultFormat = 'JSON'
    }

    dependencies {
        implementation project(":libra-core-common")

        implementation group: 'org.openjdk.jmh', name: 'jmh-core', version: jmh_version
        implementation group: 'javax.jms', name: 'javax.jms-api', version: jms_version
        implementation group: 'org.apache.activemq', name: 'artemis-jms-client', version: artemis_version
        implementation(group: 'org.apache.activemq', name: 'activemq-client', version: activemq_version) {
            exclude group: "org.apache.geronimo.specs", module: "geronimo-jms_1.1_spec"
        }

        //log
        implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
        runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j_slf4j_version
    }
}

project("libra-core") {

    sourceSets.main.java.srcDirs += ["src/transmission-api/java", "src/transmission-api/kotlin"]
    sourceSets.main.java.srcDirs += ["src/transformation-api/java", "src/transformtion-api/kotlin"]
    sourceSets.main.java.srcDirs += ["src/remediation-api/java", "src/remediation-api/kotlin"]

    configurations {
        compile.exclude group: 'org.springframework', module: 'spring-messaging'
    }

    dependencies {
        compileOnly project(":libra-aspect")
        implementation project(":libra-core-common")

        compileOnly group: 'org.aspectj', name: 'aspectjtools', version: aspectj_version

        implementation group: 'org.yaml', name: 'snakeyaml', version: snakeyaml_version

        implementation group: 'com.google.protobuf', name: 'protobuf-java', version: protobuf_version

        implementation group: 'javax.jms', name: 'javax.jms-api', version: jms_version

        compileOnly group: 'org.mybatis', name: 'mybatis', version: mybatis_version

        compileOnly group: 'org.springframework', name: 'spring-core', version: spring_version
        compileOnly group: 'org.springframework', name: 'spring-tx', version: spring_version
        compileOnly group: 'org.springframework', name: 'spring-jms', version: spring_version

        testImplementation group: 'org.mybatis.spring.boot', name: 'mybatis-spring-boot-starter', version: mybatis_starter_version
        testImplementation group: 'org.mybatis.spring.boot', name: 'mybatis-spring-boot-test-autoconfigure', version: mybatis_starter_version
        testImplementation group: 'mysql', name: 'mysql-connector-java', version: mysql_connector_version

        testImplementation group: 'com.zaxxer', name: 'HikariCP', version: hikaricp_version
        testImplementation group: 'com.h2database', name: 'h2', version: h2_version

        testImplementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: kotlin_coroutine_version

        testImplementation group: 'org.apache.activemq', name: 'artemis-jms-client', version: artemis_version
        testImplementation(group: 'org.apache.activemq', name: 'activemq-client', version: activemq_version) {
            exclude group: "org.apache.geronimo.specs", module: "geronimo-jms_1.1_spec"
        }
        testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: spring_boot_version
    }

    task testInit(type: Copy) {
        from "../database/DML/"
        into "${project.buildDir}/resources/test/"

    }

    compileTestJava.dependsOn testInit
}

project("libra-starter-config") {

    dependencies {
        implementation project(":libra-core")
        implementation project(":libra-core-common")

        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version

        // jaxb starts
        implementation group: 'javax.xml.bind', name: 'jaxb-api', version: jaxb_api_version
        runtimeOnly group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: jaxb_glassfish_version
        // jaxb ends

        compileOnly group: 'javax.jms', name: 'javax.jms-api', version: jms_version
        compileOnly group: 'org.springframework', name: 'spring-jms', version: spring_version
        compileOnly group: 'org.springframework', name: 'spring-aspects', version: spring_version

        compileOnly group: 'org.springframework.boot', name: 'spring-boot', version: spring_boot_version
        compileOnly group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version: spring_boot_version

        compileOnly group: 'org.springframework', name: 'spring-context', version: spring_version
        //spring-boot-starer contains this lib
        compileOnly group: 'org.yaml', name: 'snakeyaml', version: snakeyaml_version
        testImplementation group: 'org.yaml', name: 'snakeyaml', version: snakeyaml_version
    }
}

project("libra-starter-jms") {

    dependencies {
        implementation project(":libra-core")
        implementation project(":libra-core-common")

        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version

        compileOnly group: 'org.springframework', name: 'spring-context', version: spring_version
        compileOnly group: 'org.springframework', name: 'spring-jms', version: spring_version
        compileOnly group: 'javax.jms', name: 'javax.jms-api', version: jms_version
        //spring-boot-starer contains this lib
        compileOnly group: 'org.yaml', name: 'snakeyaml', version: snakeyaml_version

        compileOnly group: 'org.apache.activemq', name: 'artemis-jms-client', version: '2.10.0'
        compileOnly(group: 'org.apache.activemq', name: 'activemq-client', version: '5.15.10') {
            exclude group: "org.apache.geronimo.specs", module: "geronimo-jms_1.1_spec"
        }

        testCompileOnly group: 'javax.jms', name: 'javax.jms-api', version: jms_version

        testImplementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: kotlin_coroutine_version

        testImplementation group: 'org.apache.activemq', name: 'activemq-client', version: activemq_version
        testImplementation group: 'org.apache.activemq', name: 'artemis-jms-client', version: artemis_version

        testImplementation group: 'org.springframework', name: 'spring-jms', version: spring_version
        testImplementation group: 'org.yaml', name: 'snakeyaml', version: snakeyaml_version
    }
}


