plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.11' apply false
    id "org.springframework.boot" version '2.1.6.RELEASE' apply false
    id "me.champeau.gradle.jmh" version "0.4.8" apply false
    id "io.morethan.jmhreport" version "0.9.0" apply false
}

ext {
    base_version = '0.0.1-SNAPSHOT'

    kotlin_version = '1.3.11'

    commons_lang3_version = '3.7'
    druid_version = '1.1.20'
    druid_starter_version = '1.1.20'
    gson_version = '2.8.5'
    guava_version = '27.0.1-jre'
    h2_version = '1.4.197'
    hibernate_version = '5.4.0.Final'
    jmh_version = '1.21'
    junit_version = '4.12'
    kafka_client_version = '2.3.0'
    log4j_slf4j_version = '2.11.2'
    lombok_version = '1.18.4'
    mockito_kotlin_version = '1.6.0'
    mockito_version = '2.23.4'
    mybatis_version = '3.5.2'
    mybatis_spring_version = "2.0.2"
    mybatis_starter_version = '2.1.0'
    mybatis_version = '3.5.2'
    okhttp_version = '4.0.1'
    slf4j_version = '1.7.25'
    spring_version = '5.1.9.RELEASE'
    spring_boot_version = '2.1.8.RELEASE'
    spring_kafka_version = '2.2.7.RELEASE'
}

group 'xyz.ivyxjc'
version base_version

configure(allprojects) { project ->
    group = 'xyz.ivyxjc'

    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        mavenCentral()
    }

    buildDir = "${rootProject.projectDir.path}/build/${rootProject.name}/${project.name}"

    sourceSets.main.resources.srcDirs += "${rootProject.projectDir.path}/src/main/resources"
    sourceSets.test.resources.srcDirs += "${rootProject.projectDir.path}/src/test/resources"

    task fatJar(type: Jar) {
        baseName = project.name
        print(configurations)
        from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
        with jar
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8 // can be switched to 11 for testing
        targetCompatibility = JavaVersion.VERSION_1_8
        options.encoding = "UTF-8"
    }

    compileTestJava {
        sourceCompatibility = JavaVersion.VERSION_1_8  // can be switched to 11 for testing
        targetCompatibility = JavaVersion.VERSION_1_8
        options.encoding = "UTF-8"
        options.compilerArgs += "-parameters"
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-Xjsr305=strict"]
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-Xjsr305=strict"]
        }
    }

    configurations {
        all*.exclude group: 'ch.qos.logback', module: 'logback-core'
        all*.exclude group: 'ch.qos.logback', module: 'logback-classic'
        all*.exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }

    dependencies {
        testImplementation group: 'junit', name: 'junit', version: junit_version
        testImplementation(group: 'org.mockito', name: 'mockito-core', version: mockito_version) {
            exclude group: "org.hamcrest", module: "hamcrest-core"
        }
        testImplementation(group: 'com.nhaarman', name: 'mockito-kotlin', version: mockito_kotlin_version) {
            exclude module: "kotlin-stdlib"
            exclude module: "kotlin-reflect"
            exclude module: "mockito-core"
        }

        //kotlin
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version
        //apache
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: commons_lang3_version
        //log
        implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
        runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j_slf4j_version

        //guava
        implementation group: 'com.google.guava', name: 'guava', version: guava_version

        compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
        testCompileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    }
}

project("libra-core") {

    sourceSets.main.java.srcDirs += ["src/transmission-api/java", "src/transmission-api/kotlin"]
    sourceSets.main.java.srcDirs += ["src/transformation-api/java", "src/transformtion-api/kotlin"]
    sourceSets.main.java.srcDirs += ["src/remediation-api/java", "src/remediation-api/kotlin"]

    dependencies {
        implementation project(":libra-core-common")

        implementation group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version: spring_boot_version
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter', version: spring_boot_version
        implementation group: 'org.springframework', name: 'spring-jms', version: spring_version
        implementation group: 'org.springframework', name: 'spring-jdbc', version: spring_version

        implementation group: 'org.mybatis.spring.boot', name: 'mybatis-spring-boot-starter', version: '2.1.0'

        implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.17'

        implementation group: 'com.zaxxer', name: 'HikariCP', version: '3.4.0'

        implementation group: 'org.yaml', name: 'snakeyaml', version: '1.25'

        implementation group: 'org.springframework', name: 'spring-context', version: spring_version
        implementation group: 'javax.jms', name: 'javax.jms-api', version: '2.0.1'
        implementation group: 'org.apache.activemq', name: 'artemis-jms-client', version: '2.10.0'
        implementation(group: 'org.apache.activemq', name: 'activemq-client', version: '5.15.10') {
            exclude group: "org.apache.geronimo.specs", module: "geronimo-jms_1.1_spec"
        }

        implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.9.1'

        implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.3.1'


        testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: spring_boot_version
    }
}


project("libra-benchmark") {

    apply plugin: "me.champeau.gradle.jmh"
    apply plugin: "io.morethan.jmhreport"

    jmhReport {
        jmhResultPath = project.file("${rootProject.projectDir.path}/build/${rootProject.name}/${project.name}/reports/jmh/results.json")
        jmhReportOutput = project.file("${rootProject.projectDir.path}/build/${rootProject.name}/${project.name}/reports/jmh/")
    }

    jmh {
        fork = 1
        threads = 1
        warmupIterations = 3
        iterations = 5
        resultFormat = 'JSON'
    }

    dependencies {
        implementation project(":libra-core-common")

        implementation group: 'org.openjdk.jmh', name: 'jmh-core', version: '1.21'
        implementation group: 'javax.jms', name: 'javax.jms-api', version: '2.0.1'
        implementation group: 'org.apache.activemq', name: 'artemis-jms-client', version: '2.10.0'
        implementation(group: 'org.apache.activemq', name: 'activemq-client', version: '5.15.10') {
            exclude group: "org.apache.geronimo.specs", module: "geronimo-jms_1.1_spec"
        }

        //log
        implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
        runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j_slf4j_version
    }
}



